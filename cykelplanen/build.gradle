apply plugin: 'com.android.application'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':ibikecph-lib')

    compile 'com.google.android.gms:play-services-analytics:8.4.0'
}

android {
    signingConfigs {
        development {
            keyAlias secret('DEV_KEY_ALIAS')
            keyPassword secret('DEV_KEY_PASSWORD')
            storeFile file('../development-keystore.jks')
            storePassword secret('DEV_KEYSTORE_PASSWORD')
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.spoiledmilk.cykelsuperstier"
        minSdkVersion 16
        targetSdkVersion 23
        manifestPlaceholders = [HOCKEYAPP_APP_ID: ""]
        // versionCode 1
        // versionName "1.0"
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.development
            manifestPlaceholders = [HOCKEYAPP_APP_ID: "bfcca234bd157f6eba719e4be7e09445"]
        }
        release {
            minifyEnabled false
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    '../ibikecph-lib/proguard-rules.pro'
            signingConfig signingConfigs.development
        }
    }
    productFlavors {
        local {
            applicationIdSuffix ".local"
            resValue "string", "app_name", "CykelPlanen (local)"
            buildConfigField "String", "BASE_URL", "\"http://10.0.2.2:3000\""
        }
        staging {
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "CykelPlanen (staging)"
            buildConfigField "String", "BASE_URL", "\"https://staging.ibikecph.dk\""
        }
        production {
            resValue "string", "app_name", "CykelPlanen"
            buildConfigField "String", "BASE_URL", "\"https://www.ibikecph.dk\""
        }
    }
}

apply plugin: 'de.felixschulze.gradle.hockeyapp'
hockeyapp {
    apiToken = secret('HOCKEY_APP_TOKEN')
    notify = 1
    teams = 65310
    commitSha = git.head().id
    notes = git.head().fullMessage
    status = 2
}

// This line needs to be in the bottom of the file
apply plugin: 'com.google.gms.google-services'
