apply plugin: 'com.android.application'

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api project(':ibikecph-lib')
    implementation 'com.android.support:support-v4:27.1.1'
}

android {
    signingConfigs {
        development {
            keyAlias secret('DEV_KEY_ALIAS')
            keyPassword secret('DEV_KEY_PASSWORD')
            storeFile file('../debug.keystore')
            storePassword secret('DEV_KEYSTORE_PASSWORD')
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "dk.kk.cykelsuperstier"
        minSdkVersion 16
        targetSdkVersion 28
        manifestPlaceholders = [HOCKEYAPP_APP_ID: "cec6c0e595db43ffa1e9a60bfab16161"]
        versionCode 84
        versionName "2.2.4"
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.development
            manifestPlaceholders = [HOCKEYAPP_APP_ID: "cec6c0e595db43ffa1e9a60bfab16161"]
        }
        release {
            minifyEnabled false
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    '../ibikecph-lib/proguard-rules.pro'
            signingConfig signingConfigs.development
        }
    }
    flavorDimensions "default"

    productFlavors {
        local {
            applicationIdSuffix ".local"
            resValue "string", "app_name", "CykelPlanen (local)"
            buildConfigField "String", "BASE_URL", "\"http://10.0.2.2:3000\""
            dimension "default"
        }
        staging {
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "CykelPlanen (staging)"
            buildConfigField "String", "BASE_URL", "\"https://staging.ibikecph.dk\""
            dimension "default"
        }
        production {
            resValue "string", "app_name", "CykelPlanen"
            buildConfigField "String", "BASE_URL", "\"https://www.ibikecph.dk\""
            dimension "default"
        }
    }
}

apply plugin: 'de.felixschulze.gradle.hockeyapp'
hockeyapp {
    apiToken = secret('HOCKEY_APP_TOKEN')
    notify = 1
    teams = 65310
    commitSha = git.head().id
    notes = git.head().fullMessage
    status = 2
}