apply plugin: 'com.android.application'

dependencies {
    // For testing
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // This is needed to perform actions of ex the Drawer menu
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    // Optional -- UI testing with UI Automator
    //androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    api fileTree(include: ['*.jar'], dir: 'libs')
    api project(':ibikecph-lib')
    implementation 'com.google.android.gms:play-services-analytics:15.0.2'
    implementation 'com.android.support:support-v4:27.1.1'
}

android {
    signingConfigs {
        development {
            keyAlias secret('DEV_KEY_ALIAS')
            keyPassword secret('DEV_KEY_PASSWORD')
            storeFile file('../debug.keystore')
            storePassword secret('DEV_KEYSTORE_PASSWORD')
        }
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "dk.kk.ibikecph"
        minSdkVersion 16
        targetSdkVersion 27
        manifestPlaceholders = [HOCKEYAPP_APP_ID: ""]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // versionCode 1
        // versionName "1.0"
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.development
            manifestPlaceholders = [HOCKEYAPP_APP_ID: "d10e63f39cdbe7e8e3af275207388b2a"]
        }
        release {
            minifyEnabled false
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    '../ibikecph-lib/proguard-rules.pro'
            signingConfig signingConfigs.development
        }
    }

    flavorDimensions "default"

    productFlavors {
        local {
            applicationIdSuffix ".local"
            resValue "string", "app_name", "I Bike CPH (local)"
            buildConfigField "String", "BASE_URL", "\"http://10.0.2.2:3000\""
            dimension "default"
        }
        staging {
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "I Bike CPH (staging)"
            buildConfigField "String", "BASE_URL", "\"https://staging.ibikecph.dk\""
            dimension "default"
        }
        production {
            resValue "string", "app_name", "I Bike CPH"
            buildConfigField "String", "BASE_URL", "\"https://www.ibikecph.dk\""
            dimension "default"
        }
    }
}

apply plugin: 'de.felixschulze.gradle.hockeyapp'
hockeyapp {
    apiToken = secret('HOCKEY_APP_TOKEN')
    notify = 1
    teams = 65310
    commitSha = git.head().id
    notes = git.head().fullMessage
    status = 2
}
// This line needs to be in the bottom of the file
apply plugin: 'com.google.gms.google-services'
