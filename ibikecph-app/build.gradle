apply plugin: 'com.android.application'

dependencies {
    // For testing
    androidTestCompile 'com.android.support:support-annotations:23.3.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // This is needed to perform actions of ex the Drawer menu
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    // Optional -- UI testing with UI Automator
    //androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':ibikecph-lib')

    compile 'com.google.android.gms:play-services-analytics:8.4.0'
}

android {
    signingConfigs {
        development {
            keyAlias secret('DEV_KEY_ALIAS')
            keyPassword secret('DEV_KEY_PASSWORD')
            storeFile file('../development-keystore.jks')
            storePassword secret('DEV_KEYSTORE_PASSWORD')
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "dk.kk.ibikecph"
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // versionCode 1
        // versionName "1.0"
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.development
        }
        release {
            minifyEnabled false
            pseudoLocalesEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    '../ibikecph-lib/proguard-rules.pro'
            signingConfig signingConfigs.development
        }
    }
    productFlavors {
        staging {
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "I Bike CPH (staging)"
            buildConfigField "String", "API_URL", "\"https://staging.ibikecph.dk/api\""
        }
        production {
            resValue "string", "app_name", "I Bike CPH"
            buildConfigField "String", "API_URL", "\"https://www.ibikecph.dk/api\""
        }
    }
}

apply plugin: 'de.felixschulze.gradle.hockeyapp'
hockeyapp {
    apiToken = secret('HOCKEY_APP_TOKEN')
    notify = 1
    teams = 65310
    commitSha = git.head().id
    notes = git.head().fullMessage
    status = 2
}
// This line needs to be in the bottom of the file
apply plugin: 'com.google.gms.google-services'
